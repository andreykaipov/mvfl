
Integer : /[0-9]+/
        ;

Float : /[0-9]+\.[0-9]+/
      ;

Number : <Float>
       | <Integer>
       ;

PlusOp : '+'
       | '-'
       ;
MultOp : '*'
       | '/'
       | '%'
       ;
ExpnOp : '^'
       ;
Symbol : "add" | "sub" | <PlusOp>
       | "mul" | "prod" | "div" | <MultOp>
       | "pow" | <ExpnOp>
       | "min" | "max"
       ;

InfixExpr : <Factor> ( <PlusOp> <Factor> )*
          ;

Factor : <Term> ( <MultOp> <Term> )*
       ;

Term : <Base> ( <ExpnOp> <Term> )?
     ;

Base : <Number>
     | <PlusOp> <Term>
     | <PrefixExpr>
     | '(' <InfixExpr> ')'
     ;

PrefixExpr : <Symbol> <Factor>+
           ;

Expr : <InfixExpr>
     | <PrefixExpr>
     | <Symbol>
     ;

Sexpr : <Expr>*
      | '(' <Sexpr> ')'
      ;

Mvfl : /^/ <Sexpr> /$/ ;


