
Integer : /[0-9]+/
        ;

Float : /[0-9]+\.[0-9]+/
      ;

Number : <Float>
       | <Integer>
       | <PlusOp> <Number>
       ;

Space : ' ' ;

PlusOp : '+'
       | '-'
       ;
MultOp : '*'
       | '/'
       | '%'
       ;
ExpnOp : '^'
       ;
Symbol : "add" | "sum" | "sub" | "diff" | <PlusOp>
       | "mul" | "prod" | "div" | <MultOp>
       | "pow" | <ExpnOp>
       | "min" | "max"
       | "list" | "head" | "tail" | "join" | "eval"
       ;

InfixExpr : <Factor> (<Space>* <PlusOp> <Space>* <Factor>)*
          ;

Factor : <Term> (<Space>* <MultOp> <Space>* <Term>)*
       ;

Term : <Base> (<Space>* <ExpnOp> <Space>* <Term>)?
     ;

Base : <Number>
     | <Sexpr>
     ;

PrefixExpr : <Symbol> (<Space>* <Factor>)+
           ;

Expr 
     : <InfixExpr>
     | <PrefixExpr>
     | <Sexpr>
     | <Qexpr>
     | <Symbol>
     ;

Sexpr : '(' (<Space>* <Expr> <Space>*)* ')'
      | <PlusOp> <Sexpr>
      ;
Qexpr : '{' (<Space>* <Expr> <Space>*)* '}'
      ;

Mvfl  
     : /^/ (<Space>* <Expr> <Space>*)* /$/
     ;


