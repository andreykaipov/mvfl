
Integer : /[0-9]+/
        ;

Float : /[0-9]+\.[0-9]+/
      ;

Number : <Float>
       | <Integer>
       | <PlusOp> <Number>
       ;

PlusOp : '+'
       | '-'
       ;
MultOp : '*'
       | '/'
       | '%'
       ;
ExpnOp : '^'
       ;
Symbol : "add" | "sum" | "sub" | "diff" | <PlusOp>
       | "mul" | "prod" | "div" | <MultOp>
       | "pow" | <ExpnOp>
       | "min" | "max"
       | "list" | "head" | "tail" | "join" | "eval"
       ;

InfixExpr : <Factor> ( <PlusOp> <Factor> )*
          ;

Factor : <Term> ( <MultOp> <Term> )*
       ;

Term : <Base> ( <ExpnOp> <Term> )?
     ;

Base : <Number>
     | <Sexpr>
     ;

PrefixExpr : <Symbol> <Factor>+
           ;

Expr 
     : <PrefixExpr>
     | <InfixExpr>
     | <Sexpr>
     | <Qexpr>
     | <Symbol>
     ;

Sexpr : '(' <Expr>* ')'
      | <PlusOp> <Sexpr>
      ;
Qexpr : '{' <Expr>* '}'
      ;

Mvfl  
     : /^/ <Expr>* /$/
     ;


